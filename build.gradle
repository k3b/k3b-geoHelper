// Top-level build file where you can add configuration options common to all sub-projects/modules.
ext {
	// global project specific properties
	github_repository='k3b-geoHelper'
	project_version='1.1.6-SNAPSHOT'
	// project_version='1.1.5'
	project_licence_name='The Apache License, Version 2.0'

	// default for global project specific properties from file gradle.properties
	if (!hasProperty("github_userId")) github_userId='k3b'
	
	// calculated properties
	project_description="${github_userId}.${github_repository} ver ${project_version}"
	project_isReleaseVersion = !project_version.endsWith("SNAPSHOT")

	// Params for Task uploadArchives
	if (!hasProperty("mavenUpload_serverUrl")) mavenUpload_serverUrl = (project_isReleaseVersion) ? "https://oss.sonatype.org/service/local/staging/deploy/maven2/" : "https://oss.sonatype.org/content/repositories/snapshots/"
}

// reset mavenUpload_serverUrl to local if parameter is missing 
if (!hasProperty("signing.keyId") || !hasProperty("signing.password") || !hasProperty("signing.secretKeyRingFile")) {
	ext.mavenUpload_serverUrl = "file:./build/testrepository/"
	logger.info "signing.keyId=... or signing.password=... or signing.secretKeyRingFile=.. not found in gradle.properties\n"
	logger.info "Cannot upload to 'maven@https://oss.sonatype.org/...' . Using repository '${mavenUpload_serverUrl}' instead \n"
}

if (!hasProperty("mavenUpload_login") || !hasProperty("mavenUpload_password")) {
	ext.mavenUpload_serverUrl = "file:./build/testrepository/"
	logger.info "mavenUpload_login=... or mavenUpload_password=.. not found in gradle.properties\n"
	logger.info "Cannot upload to 'maven@https://oss.sonatype.org/...' . Using '${mavenUpload_serverUrl}' instead \n"
}

description = "${project_description}"

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
		
		// markdown in javadoc
		// https://github.com/Abnaxos/pegdown-doclet
        classpath 'ch.raffael.pegdown-doclet:pegdown-doclet:1.3'
    }
}

allprojects {
    repositories {
        jcenter()
        // maven { url "https://jitpack.io" } // 
    }

	// http://stackoverflow.com/questions/28450703/how-to-set-the-language-level-in-gradle-so-it-is-ide-agnostic/35827609#35827609
    tasks.withType(JavaCompile) {
        sourceCompatibility = '1.7'
        targetCompatibility = '1.7'
    } 
}

// download and generate gradlew which is not part of this repository and is required by travis
task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
    description = "(Re-)Genarate wrapper for Gradle version ${gradleVersion}."
}

task clean {
    description = "Delete the build directories."
    dependsOn { subprojects.clean }
}

task uploadArchives {
    description = "Upload ${project_description} to maven repository '${mavenUpload_serverUrl}'."
    dependsOn { subprojects.uploadArchives }
}

// markdown in javadoc
// https://github.com/Abnaxos/pegdown-doclet
apply plugin: 'ch.raffael.pegdown-doclet'

